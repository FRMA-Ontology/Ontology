prefix mlmo: <https://tw.rpi.edu/web/Courses/Ontologies/2018/FRMA/MachineLearningModelOntology/>
prefix fibo-fnd-arr-arr: <http://www.omg.org/spec/EDMC-FIBO/FND/Arrangements/Arrangements/>
prefix lio: <http://purl.org/net/lio#>
prefix lcc-lr: <http://www.omg.org/spec/LCC/Languages/LanguageRepresentation/>
prefix fibo-fnd-aap-a: <http://www.omg.org/spec/EDMC-FIBO/FND/AgentsAndPeople/Agents/>
prefix wt: <https://tw.rpi.edu/web/Courses/Ontologies/2018/FRMA/WearableThingsOntology/>
prefix frma: <https://tw.rpi.edu/web/Courses/Ontologies/2018/FRMA/FRMA/>

select ?ResultSet ?OcclusionSourceType ((?numCorrect/?total)*100 as ?accuracy)
where{
  {
  select ?ResultSet ?OcclusionSourceType (count(distinct ?classification) as ?numCorrect)
  where{
    ?ResultSet a mlmo:ResultSet .
    ?ResultSet fibo-fnd-arr-arr:hasConstituent ?Result .
    ?Result mlmo:hasFeature ?Image .

    optional{
      ?Result lcc-lr:hasTag ?classification .
      filter (?classification = ?Name)
    }
    # Add restrictions here to get specific Accuracy
    ?Image frma:hasOcclusion ?Occlusion .
    ?Occlusion a ?OcclusionSourceType .


    ?Image lio:depicts ?Person .
    ?Person fibo-fnd-aap-a:hasName ?Name
  }
  group by ?OcclusionSourceType ?ResultSet
  }
  {
  select ?ResultSet ?OcclusionSourceType (count(distinct ?Result) as ?total)
  where{
    ?ResultSet a mlmo:ResultSet .
    ?ResultSet fibo-fnd-arr-arr:hasConstituent ?Result .
    ?Result mlmo:hasFeature ?Image .

    # Add restrictions here to get specific Accuracy
    ?Image frma:hasOcclusion ?Occlusion .
    ?Occlusion a ?OcclusionSourceType .

  }
  group by ?OcclusionSourceType ?ResultSet
  }
  filter(?OcclusionSourceType != owl:NamedIndividual)
 }
order by ?ResultSet ?OcclusionSourceType
